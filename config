#!/usr/bin/env fish
# -*- mode: fish -*-

function cfg_wezterm
  cp base/wezterm.lua ~/.config/wezterm
end

function cfg_fish
  cp base/config.fish ~/.config/fish
  cp base/starship.toml ~/.config
end

function cfg_emacs -a install
  cp base/*.el ~/.config/doom
  if test -n "$install"
    set dir ~/.config/emacs
    git clone --depth 1 https://github.com/doomemacs/doomemacs $dir
    cd $dir && ./bin/doom install && ./bin/doom sync # doom upgrade
  end
end

function cfg_git
  rm ~/.gitconfig
  source base/gitconfig
  cp base/sshconfig ~/.ssh/config
end

function cfg_i3
  cat i3/i3config | string collect | \
    string replace --regex --all "^# $(hostname): " "" > ~/.config/i3/config
  cp i3/i3status.toml ~/.config/i3status-rust/config.toml
  cp i3/roficonfig.rasi ~/.config/rofi/config.rasi
end

function cfg_zathura
  cp base/zathurarc ~/.config/zathura
end

function cfg_lilypond -a lpver
  set re '^(?<long>(?<short>\d\.\d{1,2})\.\d{1,2})$'
  if not string match --quiet --regex $re $lpver
    echo argparse: missing or invalid LilyPond version && return 1
  end
  set url https://lilypond.org/download/sources/v$short/lilypond-$long.tar.gz
  set dir ~/.config/lilypond
  set build $dir/lilypond-$long/build
  curl -fsSL $url | tar xvz -C $dir && \
  mkdir $build && cd $build && ../autogen.sh --noconfigure && \
  ../configure --prefix=$dir --disable-documentation GUILE_FLAVOR=guile-3.0 && \
  make -j 8 && make install
end

function cfg_foundry
  set dir ~/.config/foundry/bin
  mkdir -p $dir
  set release https://github.com/foundry-rs/foundry/releases/download
  set url $release/stable/foundry_stable_linux_amd64.tar.gz
  curl -fsSL $url | tar xvz -C $dir
  forge completions fish > ~/.config/fish/completions/forge.fish
  cast completions fish > ~/.config/fish/completions/cast.fish
  anvil completions fish > ~/.config/fish/completions/anvil.fish
end

argparse init a/all i/install v/version= -- $argv || return

if set -ql _flag_init
  mkdir -p ~/.config/{wezterm,fish,doom,i3,i3status-rust,rofi}
  mkdir -p ~/.config/{zathura,lilypond,foundry} ~/.ssh
end

if set -ql _flag_all
  cfg_wezterm; cfg_fish; cfg_emacs $_flag_install; cfg_git; cfg_i3;
  cfg_zathura && return
end

for target in $argv
  switch $target
  case wezterm; cfg_wezterm
  case fish; cfg_fish
  case emacs; cfg_emacs $_flag_install
  case git; cfg_git
  case i3; cfg_i3
  case zathura; cfg_zathura
  case lilypond; cfg_lilypond $_flag_version
  case foundry; cfg_foundry
  case '*'
    echo argparse: unknown target $target && return 1
  end
end
